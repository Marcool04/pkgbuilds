# Maintainer: Logan Sevcik <logan+aur@sevcik.email>
pkgname=nqptp-git
pkgver=1.2.5.dev.r16.g32bfe78
pkgrel=1
pkgdesc="A daemon that monitors timing data from PTP clocks"
arch=('i686' 'pentium4' 'x86_64' 'arm' 'armv6h' 'armv7h' 'aarch64' 'riscv64' 'powerpc64le')
url="https://github.com/mikebrady/nqptp#readme"
license=('GPL2')
provides=(nqptp)
conflicts=(nqptp)
replaces=(nqptp)
backup=()
options=()
source=("git+https://github.com/mikebrady/nqptp.git" 'nqptp.sysusers')
noextract=()
sha256sums=('SKIP'
            '7b9f14ae70bdc6151fc879d22e3ce9c27eef9233936ef690d31daface3f31b52')

_gitname="nqptp"

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  # Disable creation of users in the Makefile
  # https://github.com/mikebrady/nqptp/issues/29#issuecomment-1780201003
  cd "$srcdir/$_gitname"
  sed -i 's/\(.* groupadd \)/# \1/g' Makefile.am
  sed -i 's/\(.* useradd \)/# \1/g' Makefile.am
}

build() {
  cd "$srcdir/$_gitname"
  autoreconf -fi
  ./configure --prefix=/usr --with-systemd-startup
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir/" install

  # Install the sysusers file for creating the corresponding users
  # https://github.com/mikebrady/nqptp/issues/29#issuecomment-1780201003
  install -Dm644 "$srcdir/nqptp.sysusers" "$pkgdir/usr/lib/sysusers.d/nqptp.conf"
}
